import groovy.util.Node
import org.gradle.api.publish.maven.MavenPublication
import org.gradle.api.tasks.bundling.Jar
/*
 * Copyright (c) 2016-2019, Leftshift One
 * __________________
 * [2019] Leftshift One
 * All Rights Reserved.
 * NOTICE:  All information contained herein is, and remains
 * the property of Leftshift One and its suppliers,
 * if any.  The intellectual and technical concepts contained
 * herein are proprietary to Leftshift One
 * and its suppliers and may be covered by Patents,
 * patents in process, and are protected by trade secret or copyright law.
 * Dissemination of this information or reproduction of this material
 * is strictly forbidden unless prior written permission is obtained
 * from Leftshift One.
 */

buildscript {
    ext {
        kotlinVersion = "1.3.41"
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.17"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
        classpath "com.netflix.nebula:nebula-release-plugin:8.0.3"
    }
}

apply plugin: "kotlin"
apply plugin: "org.jetbrains.dokka"
apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"


repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile "org.antlr:antlr4-runtime:4.7.2"
    
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.5.1'
    testCompile "org.assertj:assertj-core:3.11.1"
}

sourceSets.main.java.srcDirs += new File(buildDir, "generated-src/antlr")

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = "javadoc"
    outputDirectory = "${buildDir}/dokkaJavadoc"
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    from dokkaJavadoc
    classifier "javadoc"
}

test {
    useJUnitPlatform()
}

artifacts {
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.name
            from components.java
            artifact sourceJar
            artifact javadocJar
            pom.withXml {
                Node root = asNode()
                root.appendNode("name", "dynabuffers")
                root.appendNode("description", "dynabuffers")
                root.appendNode("url", "https://github.com/leftshiftone/dynabuffers")
                
                Node licenses = root.appendNode("licenses")
                Node license = licenses.appendNode("license")
                license.appendNode("name", "The Apache License, Version 2.0")
                license.appendNode("url", "http://www.apache.org/licenses/LICENSE-2.0.txt")
                
                Node developers = root.appendNode("developers")
                Node developer = developers.appendNode("developer")
                developer.appendNode("id", "leftshiftone")
                developer.appendNode("name", "Leftshift One")
                developer.appendNode("email", "contact@leftshift.one")
                developer.appendNode("organization", "Leftshift One Software GmbH")
                developer.appendNode("organizationUrl", "https://www.leftshift.one")
                
                Node scm = root.appendNode("scm")
                scm.appendNode("connection", "scm:git:git://github.com/leftshiftone/dynabuffers.git")
                scm.appendNode("developerConnection", "scm:git:ssh://github.com:leftshiftone/dynabuffers.git")
                scm.appendNode("url", "https://github.com/leftshiftone/dynabuffers")
            }
        }
    }
}

bintray {
    logger.info "Bintray deploy with user [${System.getenv('BINTRAY_USER')}] while working on ${project.name}"
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publish = true
    pkg {
        repo = "dynabuffers"
        name = "${project.group}.${project.name}"
        userOrg = "leftshiftone"
        licenses = ["Apache-2.0"]
        vcsUrl = "https://github.com/leftshiftone/dynabuffers.git"
        publications = ["mavenJava"]
    }
}
